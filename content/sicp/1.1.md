---
title: "SICP 1.1 Exercises"
date: 2020-01-15T16:08:23+01:00
categories:
- "SICP"
tags:
- "Learning"
- "SICP"
- "Examples"
---
As to be expected from the introduction of an introductory book, things started out fairly straightforward and I don’t think I had any major problems with this chapter.

Exercises
-------------------------
Exercise 1.1

Below is a sequence of expressions. What is the result printed by the interpreter in response to each expression? Assume that the sequence is to be evaluated in the order in which it is presented.

{{< highlight scheme >}}
10 ;10
(+ 5 3 4) ;12
(- 9 1) ;8
(/ 6 2) ;3
(+ (* 2 4) (- 4 6)) ;6
(define a 3) ;a
(define b (+ a 1)) ;b
(+ a b (* a b)) ;19
(= a b) ;#f

(if (and (> b a) (< b (* a b))) 
    b
    a)
;4

(cond ((= a 4) 6) 
      ((= b 4) (+ 6 7 a))
      (else 25))
;16

(+ 2 (if (> b a) b a)) 
;6

(* (cond ((> a b) a) 
         ((< a b) b)
         (else -1))
   (+ a 1))		
;16
{{< /highlight >}}



Exercise 1.2

Translate the following expression into prefix form:

$$\frac{5 + 4 + (2 - (3 - (6 + \frac{4}{5})))}{3(6 - 2)(2 - 7)}$$

{{< highlight scheme >}}
(/ (+ 5 4 (- 2 (- 3 (+ 6 (/ 4 5))))) 
   (* 3 (- 6 2) (- 2 7)))
{{< /highlight >}}


Exercise 1.3

Define a procedure that takes three numbers as arguments and returns the sum of the squares of the two larger numbers.
{{< highlight scheme >}}
(define (square a) (* a a))
(define (smallest a b c)
        (cond ((and (< a b) (< a c)) a)
              ((and (< b a) (< b c)) b)
              (else c)))
(define (sumlargestsquares a b c)
        (cond ((= a (smallest a b c)) (+ (square b) (square c)))
              ((= b (smallest a b c)) (+ (square a) (square c)))
              (else (+ (square a) (square b)))))
{{< /highlight >}}

I realise here that I don’t need to separately define smallest as I’m only asking here if something is equal to it and I could combine the two procedures easily. This is just what I initially did.


Exercise 1.4
Observe that our model of evaluation allows for combinations whose operators are compound expressions. Use this observation to describe the behavior of the following procedure
{{< highlight scheme >}}
(define (a-plus-abs-b a b)
  ((if (> b 0) + -) a b))
{{< /highlight >}}

If $b > 0$ then this evalutes to (+ a b) by substitution. 
If $b ≤ 0$ then this evalutes to (- a b). However since $b$ is negative then $a - (-\lvert b \rvert)$ gives $(a + \lvert b \rvert)$. So this procedure is $a$ plus the absolute value of $b$.



Exercise 1.5

Ben Bitdiddle has invented a test to determine whether the interpreter he is faced with is using applicative-order evaluation or normal-order evaluation. He defines the following two procedures:
{{< highlight scheme >}}
(define (p) (p))
(define (test x y) 
  (if (= x 0) 
      0 
      y))
;Then he evaluates the expression 
(test 0 (p))
{{< /highlight >}}
What behavior will Ben observe with an interpreter that uses applicative-order evaluation? What behavior will he observe with an interpreter that uses normal-order evaluation? Explain your answer. (Assume that the evaluation rule for the special form if is the same whether the interpreter is using normal or applicative order: The predicate expression is evaluated first, and the result determines whether to evaluate the consequent or the alternative expression.)

In normal-order evalutation we will be first checking (if (= 0 0) 0 (p)) which will evaluate to 0. In applicative-order evaluation we will try and resolve (p) first but (p) just calls itself in a loop and so the program cannot finish.


Exercise 1.6

Alyssa P. Hacker doesn’t see why if needs to be provided as a special form. “Why can’t I just define it as an ordinary procedure in terms of cond?” she asks. Alyssa’s friend Eva Lu Ator claims this can indeed be done, and she defines a new version of if:
{{< highlight scheme >}}
(define (new-if predicate 
                then-clause 
                else-clause)
  (cond (predicate then-clause)
        (else else-clause)))
;Eva demonstrates the program for Alyssa:
(new-if (= 2 3) 0 5)
5
(new-if (= 1 1) 0 5)
0
;Delighted, Alyssa uses new-if to rewrite the square-root program:
(define (sqrt-iter guess x)
  (new-if (good-enough? guess x)
          guess
          (sqrt-iter (improve guess x) x)))
{{< /highlight >}}

What happens when Alyssa attempts to use this to compute square roots? Explain.

As the new-if is not a special form like the regular if it will be evaluated in applicative order. This results in an infinite loop as the recursive (sqrt-iter) is calculated before the new-if is worked through.


Exercise 1.7 

The good-enough? test used in computing square roots will not be very effective for finding the square roots of very small numbers. Also, in real computers, arithmetic operations are almost always performed with limited precision. This makes our test inadequate for very large numbers. Explain these statements, with examples showing how the test fails for small and large numbers.

For a small number a tolerance of 0.001 is not sufficient. For example (sqrt 0.0001) returns 0.03230844833048122 instead of the expected result of 0.01. If you then square this number you will get 0.00104383583. Then our check of 0.00104383583–0.0001 is within our tolerance but has not given us a good enough answer. For a large number if the calculation precision doesn’t come close enough we don’t have a satisfied end condition and the program won’t stop running as the guesses cannot get close enough to the required level.

An alternative strategy for implementing good-enough? is to watch how guess changes from one iteration to the next and to stop when the change is a very small fraction of the guess. Design a square-root procedure that uses this kind of end test. Does this work better for small and large numbers?

{{< highlight scheme >}}
(define (sqrt-iter guess x)
  (if (good-enough? guess x)
      guess
      (sqrt-iter (improve guess x) x)))
(define (improve guess x)
  (average guess (/ x guess)))
(define (average x y) 
  (/ (+ x y) 2))
(define (good-enough? guess x)
  (< (abs (- guess (improve guess x))) (* guess 0.001)))
(define (sqrt x)
  (sqrt-iter 1.0 x))
{{< /highlight >}}
This gives (sqrt 0.0001) as 0.010000714038711746. For larger numbers the tolerance increases as it scales with the guess.


Exercise 1.8

Newton’s method for cube roots is based on the fact that if y is an approximation to the cube root of x , then a better approximation is given by the value

$$\frac{\frac{x}{y^2} + 2y}{3}$$

Use this formula to implement a cube-root procedure analogous to the square-root procedure. (In 1.3.4 we will see how to implement Newton’s method in general as an abstraction of these square-root and cube-root procedures.)

{{< highlight scheme >}}
(define (square x) (* x x))
(define (cbrt-iter guess x)
  (if (good-enough? guess x)
      guess
      (cbrt-iter (improve guess x) x)))
(define (improve guess x)
  (/ (+ (/ x (square guess)) (* 2 guess)) 3))
(define (good-enough? guess x)
  (< (abs (- guess (improve guess x))) (* guess 0.001)))
(define (cbrt x)
  (cbrt-iter 1.0 x))
{{< /highlight >}}
